@echo off
setlocal enabledelayedexpansion enableextensions
cls
@rem File name: C:\firebird\QA\fbt-repo\fbt-run.bat 
@rem It is recommended to put this file in the 'top-level' folder - "QA\fbt-repo".
@rem Running sample: C:\MIX\firebird\QA\fbt-repo\fbt-run.bat core_NNNN.fbt 
@rem Also it is convenient to 'bind' this batch to files with pattern *.fbt
@rem from sub-folder fbt-repo\tests\ (FAR: F10 / Commands / File associations)
@rem I that case one may always to sit in the folder with .fbt files
@rem (e.g. fbt-repo\tests\bugs\) and run all necessary tests only by pressing
@rem ENTER key on selected .fbt

cd >tmp.tmp.tmp
set /p curdir= < tmp.tmp.tmp
del tmp.tmp.tmp

set cdprefix=
set find_what=%~dp0
set repl_with=
for /f "tokens=* delims=\" %%a in ("%curdir%") do (
  echo %%a
  set string=%%a
  set cdprefix=!string:%find_what%=%repl_with%!
  set cdprefix=!cdprefix:tests\=%repl_with%!
  set cdprefix=!cdprefix:\=.!
)

set fbt_name=%~n1
set fb25home=C:\MIX\firebird\fb25\bin
set fb30home=C:\MIX\firebird\fb30
set fb25port=3050
set fb30port=3333

@echo off
cd %~dp0

set overall=0
set e25size=0
set e30size=0

:run25
  echo %date%, %time%, running test: %fbt_name%>fbt-run.25.log
  echo ------------------------------------------------------->>fbt-run.25.log
  %fb25home%\fbsvcmgr localhost/%fb25port%:service_mgr user %isc_user% password %isc_password% info_server_version 1>>fbt-run.25.log 2>&1
  set fbt_run=fbt_run -b %fb25home% %cdprefix%.%fbt_name% -o localhost/%fb25port%
  echo Command: %fbt_run%>>fbt-run.25.log
  echo Running for 2.5: %fbt_run%
  cmd /c %fbt_run% 1>>fbt-run.25.log 2>fbt-run.25.err

  findstr /B /I "Nothing to run" fbt-run.25.log 1>nul 2>&1
  if errorlevel 1 goto chkr25
  set overall=1
  goto showlog

:chkr25
  findstr /B /I "FAIL ERROR" fbt-run.25.log 1>nul 2>&1
  if errorlevel 1 goto run30
  set overall=1
  goto showlog

:run30
  echo %date%, %time%, running test: %fbt_name%>fbt-run.30.log
  echo ------------------------------------------------------->>fbt-run.30.log
  %fb30home%\fbsvcmgr localhost/%fb30port%:service_mgr user %isc_user% password %isc_password% info_server_version 1>>fbt-run.30.log 2>&1
  echo.

  set fbt_run=fbt_run -b %fb30home% %cdprefix%.%fbt_name% -o localhost/%fb30port%
  echo Command: %fbt_run%>>fbt-run.30.log
  echo Running for 3.0: %fbt_run%
  cmd /c %fbt_run% 1>>fbt-run.30.log 2>fbt-run.30.err

  findstr /B /I "Nothing to run" fbt-run.30.log 1>nul 2>&1
  if errorlevel 1 goto chkr30
  set overall=1
  goto showlog


:chkr30
  findstr /B /I "FAIL ERROR" fbt-run.30.log 1>nul 2>&1
  if errorlevel 1 goto chk_err
  set overall=1
  goto showlog

:chk_err
  for /f "usebackq" %%A in ('fbt-run.30.err') do set e30size=%%~zA
  if .%e30size%.==.. set e30size=0
  if %e30size% gtr 0 (
    set overall=1
  )

  for /f "usebackq" %%A in ('fbt-run.25.err') do set e25size=%%~zA
  if .%e25size%.==.. set e25size=0
 if %e25size% gtr 0 (
   set overall=1
  )

:showlog

echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Log25: start  ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|
type fbt-run.25.log
echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Log25: finish ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|

echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Log30: start  ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|
type fbt-run.30.log
echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Log30: finish ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|

if .%overall%.==.0. (
  echo.
  echo ###  A L L    O K  ###
  del fbt-run.30.err
  del fbt-run.25.err
  del fbt-run.25.log
  del fbt-run.30.log
) else (
  if .%e25size%. gtr .0. (
    echo.
    echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Err25: start  ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|
    type fbt-run.25.err
    echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Err25: finish ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|
  ) else (
    del fbt-run.25.err
  )
  echo.

  if .%e30size%. gtr .0. (
    echo.
    echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Err30: start  ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|
    type fbt-run.30.err
    echo ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^| Err30: finish ^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|^|
  ) else (
    del fbt-run.30.err
  )
  echo.
  echo ###   A C H T U N G  ###  T E S T    F A I L E D  ###
  echo.
  echo Check results:
  fbt_view -d %~dp0results.trf 

)
cd /d %curdir%
