@echo off
setlocal enabledelayedexpansion enableextensions
set fbt_tests_root=C:\MIX\firebird\QA\fpt-repo\tests\
set file_ext=^<fbt_file^>
set git_text=Updated "!file_ext!": adjust expected stdout/stderr to current FB version.
if .%1.==.. goto syntax

set fbt_file=%1

if NOT .%2.==.. (
    set /a i=0
    @rem echo all inp arguments: ^|"%*"^|
    for /f "tokens=1* delims= " %%a in ("%*") do (
        set customer_comment=%%b
    )
    @rem echo customer  comment: ^|!customer_comment!^|

    set git_text=Added/Updated "!file_ext!": !customer_comment!
)

set log=%~dpn0.log
del !log! 2>nul

for /f %%a in ("!fbt_file!") do (
    @rem set file_ext=%%~nxa
    set file_ext=%%a
    set text_chk=!file_ext:%fbt_tests_root%=!
    if .!text_chk!.==.!file_ext!. (
        @rem .fbt was specified WITHOUT path
        set file_ext=%cd%\!file_ext!
    ) 
    set file_ext=!file_ext:%fbt_tests_root%=!

)
set git_text=!git_text:^<fbt_file^>=%file_ext%!

(
    echo Log for: %~f0 !fbt_file!
    echo Created !date! !time! on host '%COMPUTERNAME%'
    echo.
    echo Comment is: git_text=!git_text!
) >>!log!

set msg=Processing command: git add %1
echo !msg!
echo !msg!>>!log!

@rem ############################
@rem ###    g i t    a d d    ###
@rem ############################
git add %1 1>>!log! 2>&1
set elevel=!errorlevel!
echo elevel=!elevel!
echo elevel=!elevel!>>!log!

git status %1 1>>!log! 2>&1

set msg=Processing command: git commit -a -m "!git_text!"
echo !msg!
echo !msg!>>!log!

@rem ##################################
@rem ###    g i t    c o m m i t    ###
@rem ##################################
git commit -a -m "!git_text!" 1>>!log! 2>&1
set elevel=!errorlevel!
echo elevel=!elevel!
echo elevel=!elevel!>>!log!


set msg=Processing command: git push 
echo !msg!
echo !msg!>>!log!

@rem ##############################
@rem ###    g i t    p u s h    ###
@rem ##############################
git push 1>>!log! 2>&1
set elevel=!errorlevel!
echo elevel=!elevel!
echo elevel=!elevel!>>!log!

echo -------------------------------
type !log!
echo -------------------------------

@rem #######################################
@rem ###    s e n d i n g     m a i l    ###
@rem #######################################

call %~dp0qa-sendmail.bat "!git_text!" !log!

echo Check:
git config --get remote.origin.url
@rem https://github.com/FirebirdSQL/fbt-repository.git

goto final

:syntax
    echo.
    echo Syntax: 
    echo 1. %~f0 ^<fbt_file^>
    echo.
    echo.    Commit will be done with comment:
    echo.    !git_text!
    echo.
    echo.
    echo 2. %~f0 ^<fbt_file^> some very clever comment here
    echo.
    echo.    Commit will be done with comment:
    echo.    some very clever comment here
    echo.
    pause
    goto final
:final
    echo Bye-bye from %~f0

